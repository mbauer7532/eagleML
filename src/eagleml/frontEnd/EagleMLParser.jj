options {
  JAVA_UNICODE_ESCAPE = true;
  STATIC = false;
}

PARSER_BEGIN(EagleMLParser)

package eagleml.frontEnd;

import eagleml.ast.EagleMLAst.*;
import eagleml.ast.EagleMLTypes.*;
import eagleml.ast.Operators.Operator;

import java.util.List;
import java.util.Deque;
import java.util.ArrayDeque;

final public class EagleMLParser {
  public void initExpressionStacks() {
    m_operators = new ArrayDeque<Operator>();
    m_operands  = new ArrayDeque<ExprAst>();

    return;
  }

  private void popOperator() {
    final Operator oper = m_operators.pop();

    if (oper.isBinary()) {
      final ExprAst t1 = m_operands.pop();
      final ExprAst t0 = m_operands.pop();

      m_operands.push(BinOper.create(t0, oper, t1));
    }
    else if (oper.isUnary()) {
      final ExprAst t0 = m_operands.pop();
      m_operands.push(UnaryOper.create(oper, t0));
    }
    else
    {
      throw new RuntimeException("Bad operator.");
    }

    return;
  }

  private void pushOperator(final Operator oper) {
    while (m_operators.peek().greaterPrecedence(oper)) {
      popOperator();
    }
    m_operators.push(oper);

    return;
  }

  private Deque<Operator> m_operators;
  private Deque<ExprAst> m_operands;
}

PARSER_END(EagleMLParser)

SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN : /* RESERVED WORDS AND LITERALS */
{
  < BOOLEAN: "bool" >
| < INT: "int" >
| < TRUE: "true" >
| < FALSE: "false" >
| < IF: "if" >
| < THEN: "then" >
| < ELSE: "else" >
| < DEF: "def" >
| < VAL: "val" >
| < LET: "let" >
| < IN: "in" >
| < END: "end" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
| < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
| < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
| < #OCTAL_LITERAL: "0" (["0"-"7"])* >
| < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"] >
| < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
| < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'" >
| < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\"" >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
| < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ] >
| < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ] >
}

TOKEN : /* SEPARATORS */
{
  < LPAREN:    "(" >
| < RPAREN:    ")" >
| < LBRACE:    "{" >
| < RBRACE:    "}" >
| < LBRACKET:  "[" >
| < RBRACKET:  "]" >
| < SEMICOLON: ";" >
| < COMMA:     "," >
| < DOT:       "." >
}

TOKEN : /* OPERATORS */
{
  < ASSIGN:               "=" >
| < GT:                   ">" >
| < LT:                   "<" >
| < BANG:                 "!" >
| < TILDE:                "~" >
| < QUESTION_MARK:        "?" >
| < COLON:                ":" >
| < EQ:                   "==" >
| < NEQ:                  "!=" >
| < LEQ:                  "<=" >
| < GEQ:                  ">=" >
| < OR:                   "||" >
| < AND:                  "&&" >
| < PLUS:                 "+" >
| < MINUS:                "-" >
| < TIMES:                "*" >
| < DIVIDE:               "/" >
| < BIT_AND:              "&" >
| < BIT_OR:               "|" >
| < XOR:                  "^" >
| < REM:                  "%" >
| < LSHIFT:               "<<" >
| < RIGHT_SIGNED_SHIFT:   ">>" >
| < RIGHT_UNSIGNED_SHIFT: ">>>" >
}

/********************************************
 * THE EagleML LANGUAGE GRAMMAR STARTS HERE *
 ********************************************/

/*
 * Program structuring syntax follows.
 */

DefinitionList CompilationUnit() :
{
  DefinitionList defList;
}
{
  defList = CompilationUnitAux() <EOF>
  {
    return defList;
  }
}

DefinitionList CompilationUnitAux() :
{
  DefinitionList defList = new DefinitionList();
  Def def;
}
{
  ((def = VariableDefinition() | def = FunctionDefinition()) { defList.add(def); } )+
  {
    return defList;
  }
}

VarDef VariableDefinition() :
{
  String varName;
  EagleMLType varTyp;
  ExprAst expr;
}
{
  <VAL> <IDENTIFIER> { varName = token.image; } <COLON> varTyp = Type() <ASSIGN> expr = Expr()
  {
    return VarDef.create(varName, varTyp, expr);
  }
}

FunDef FunctionDefinition() :
{
   String funName;
   TypedVarList tyVars;
   ExprAst expr;
   EagleMLType funType = null;
}
{
    <DEF>
    <IDENTIFIER> { funName = token.image; }
    <LPAREN>
    tyVars = FunArgs()
    <RPAREN>
    <COLON>
    funType = Type()
    <ASSIGN>
    expr = Expr()
    {
      return FunDef.create(funName, tyVars, funType, expr);
    }
}

TypedVarList FunArgs() :
{
  TypedVar typedVar;
  TypedVarList typedVars = new TypedVarList();
}
{
  [ typedVar = FormalParameter() { typedVars.add(typedVar); }
    (<COMMA> typedVar = FormalParameter() { typedVars.add(typedVar); })*]
  { return typedVars; }
}

TypedVar FormalParameter() :
{
  EagleMLType typ;
  String name;
}
{
  (<IDENTIFIER> { name = token.image; } <COLON> typ = Type()) { return TypedVar.create(name, typ); }
}

EagleMLType Type() :
{
  EagleMLType typ;
}
{
  (<INT>       { typ = IntPrimitiveType.create(); }
   | <BOOLEAN> { typ = BoolPrimitiveType.create(); })
  { return typ; }
}

/*
 * Expression syntax follows.
 */

ExprAst Expr() :
{
  int operatorStackSize, operandStackSize;
}
{
  {
    operatorStackSize = m_operators.size(); // Only used in the assert below.
    operandStackSize  = m_operands.size();  // Only used in the assert below.

    m_operators.push(Operator.NO_OPPER);
  }
  ExprAstAux()
  {
    final ExprAst res = m_operands.pop();
    final Operator oper = m_operators.pop(); // Get rid of the sentinel.

    assert oper == Operator.NO_OPPER:
           "Sentinel operator was expected but was not found.";
    assert m_operators.size() == operatorStackSize:
           "The expression operands array was not empty at the end of an expression parse.";
    assert m_operands.size() == operandStackSize:
           "The expression operators array was not empty at the end of an expression parse.";

    return res;
  }
}

void ExprAstAux() :
{
  Operator oper;
}
{
  (P()
   (LOOKAHEAD(1) oper = BinaryOperator() { pushOperator(oper); } P())*)
  {
    while (! m_operators.peek().isNoOper())
    {
      popOperator();
    }
  }
}

void P() :
{
  Operator unary;
  ExprAst condExpr, thenExpr, elseExpr;
  DefinitionList letBindings;
  ExprAst letExpr;
  ExprAst expr = null;
  ExprList exprList = null;
  String name;
}
{
 (<LPAREN> expr = Expr() <RPAREN>
  | <INTEGER_LITERAL>
    {
      expr = IntLit.create(Integer.parseInt(token.image));
    }
  | <TRUE>
    {
      expr = BoolLit.create(true);
    }
  | <FALSE>
    {
      expr = BoolLit.create(false);
    }
  | <IDENTIFIER> { name = token.image; } [<LPAREN> exprList = ExprList() <RPAREN>]
    {
      expr = exprList == null
             ? VarRef.create(name)
             : FunCall.create(name, exprList);
    }
  | unary = UnaryOperator()
    {
      m_operators.push(unary);
    }
    P()
  | <IF> condExpr = Expr() <THEN> thenExpr = Expr() <ELSE> elseExpr = Expr()
    {
      expr = IfExpr.create(condExpr, thenExpr, elseExpr);
    }
  | <LET> letBindings = CompilationUnitAux() <IN> letExpr = Expr() <END>
    {
      expr = LetExpr.create(letBindings, letExpr);
    })
  {
    if (expr != null)
    {
      m_operands.push(expr);
    }
  }
}

ExprList ExprList() :
{
  ExprList exprList = new ExprList();
  ExprAst expr;
}
{
  [expr = Expr() { exprList.add(expr); }
   (<COMMA> expr = Expr() { exprList.add(expr); })*]
  {
    return exprList;
  }
}

Operator BinaryOperator() :
{
  Operator oper;
}
{
  (<OR>  { oper = Operator.OR; }
   | <AND> { oper = Operator.AND; }

   | <LT>     { oper = Operator.LT; }
   | <GT>     { oper = Operator.GT; }
   | <LEQ>    { oper = Operator.LEQ; }
   | <GEQ>    { oper = Operator.GEQ; }
   | <EQ>     { oper = Operator.EQ; }
   | <NEQ>    { oper = Operator.NEQ; }

   | <PLUS>   { oper = Operator.PLUS; }
   | <MINUS>  { oper = Operator.MINUS; }
   | <TIMES>  { oper = Operator.TIMES; }
   | <DIVIDE> { oper = Operator.DIVIDE; }
   | <REM>    { oper = Operator.REM; }
   | <XOR>    { oper = Operator.XOR; })
  {
    return oper;
  }
}

Operator UnaryOperator() :
{
  Operator oper;
}
{
  (<PLUS>  { oper = Operator.UPLUS; }
   | <MINUS> { oper = Operator.UMINUS; })
  {
    return oper;
  }
}
